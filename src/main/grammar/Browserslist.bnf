{
  generate=[java="8"]
  parserClass="com.github.aleksandrsl.intellijbrowserslist.parser.BrowserslistParser"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Browserslist"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.aleksandrsl.intellijbrowserslist.psi"
  psiImplPackage="com.github.aleksandrsl.intellijbrowserslist.psi.impl"

  elementTypeHolderClass="com.github.aleksandrsl.intellijbrowserslist.psi.BrowserslistTypes"
  elementTypeClass="com.github.aleksandrsl.intellijbrowserslist.psi.BrowserslistElementType"
  tokenTypeClass="com.github.aleksandrsl.intellijbrowserslist.psi.BrowserslistTokenType"

  tokens=[
    EOL="regexp:\R"
    LBRACKET="["
    RBRACKET="]"
    IN="in"
    PERCENT="regexp:\d*\.?\d+%"
    DEFAULTS="defaults"
    COMPARE="regexp:>=?|<=?"
    space="regexp:\s+"
    LAST="last"
    INTEGER="regexp:\d+"
    VERSIONS="regexp:versions?"
    YEARS="years"
    FLOAT="regexp:\d*\.?\d+"
  ]
}
browserslistFile ::= (EOL|COMMENT)* (queries_ (EOL sections_)?|sections_)


meta eol_separated_items ::= <<item>> (EOL <<item>>|COMMENT|EOL !'[')*

private sections_ ::= <<eol_separated_items section>>
section ::= '[' IDENTIFIER+ ']' COMMENT? (EOL queries_)?

private queries_ ::= <<eol_separated_items queryExpression_>>
private queryExpression_ ::= NOT? query_ ((AND|OR) NOT? query_)*
private query_ ::=
    statsQuery
    | lastQuery
    | timeQuery
    | unreleasedQuery
    | supportsQuery
    | targetQuery
    | extendsQuery
    | DEAD
    | DEFAULTS

// TODO: Cover in custom stats like `cover 5% in browserslist-config-mycompany stats` should be supported?
statsQuery ::= (COMPARE|COVER) PERCENT (IN STATS)?
lastQuery ::= LAST INTEGER TARGET? MAJOR? VERSIONS
unreleasedQuery ::= UNRELEASED TARGET? VERSIONS
timeQuery ::= (LAST (FLOAT|INTEGER) YEARS) | (SINCE TIME|INTEGER)
supportsQuery ::= SUPPORTS FEATURE
targetQuery ::=
    TARGET (COMPARE? TARGET_VERSION | TARGET_VERSIONS_RANGE)
    | CURRENT_NODE_VERSION TARGET
    | MAINTAINED_NODE_VERSIONS TARGET VERSIONS
extendsQuery ::= EXTENDS IDENTIFIER